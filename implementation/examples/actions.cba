-- Graphviz handler for DAG generic effects
do for <- return (rec for fl ->
  do g <- fst fl in
  do l <- snd fl in
  if isnil l then return () else (g (head l) ; for (g, tail l)))
in
with handler {
  act as k ->
    do a <- fst as in
    do s <- snd as in
    do b <- !unique() in
    (!print ("n" ++ hash b ++ " [label=\"" ++ s ++ "\"];");
    for (fun a -> !print ("n" ++ hash a ++ " -> n" ++ hash b ++ ";"), a);
    k b)
} handle (
  do hi <- !act(nil (),"hello") in
  do o  <- !act(cons (hi, nil ()),"world") in
  do a  <- !act(nil (),"a") in
  do b  <- !act(nil (),"b") in
  do c  <- !act(cons (a, cons (b, nil ())),"c") in
  do d  <- !act(cons (b, nil ()),"d") in
  return ()
)
