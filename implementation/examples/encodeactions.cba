-- Graphviz handler for DAG generic effects
do for <- return rec for f l ->
  if null l then return () else (f (head l) ; for f (tail l))
in
do concat <- return rec concat v1 v2 ->
  if null v1 then return v2 else head v1 :: concat (tail v1) v2
in
with handler {
  act as k ->
    do a <- fst as in
    do s <- snd as in
    do b <- !unique () in (
      !print ("n" ++ hash b ++ " [label=\"" ++ s ++ "\"]; ") ;
      for (fun a -> !print ("n" ++ hash a ++ " -> n" ++ hash b ++ "; ")) a ;
      k (b :: [])
    ),
  zero z k -> k [],
  plus v k -> k (concat (fst v) (snd v))
} handle (
  do z  <- !zero () in
  do hi <- !act (z,"hello") in
  do o  <- !act (hi,"world") in
  do a  <- !act (z,"a") in
  do b  <- !act (z,"b") in
  do ab <- !plus (a,b) in
  do c  <- !act (ab,"c") in
  do d  <- !act (b,"d") in
  return ()
)
